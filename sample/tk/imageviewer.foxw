import foxtk
import image

def ZOOM_SCALE = [0.25, 0.5, 0.75, 1.0, 1.5, 2.0, 4.0]

class MyForm : Form
{
    var m_fname
    var m_zoom
    var m_pane
    var m_image

    new(fname) {
        this.title = Str(fname)
        m_pane = ImagePane(this)
        m_image = Image.load(fname)

        m_zoom = 3
        m_pane.image = image_zoom(m_image, ZOOM_SCALE[m_zoom])
        this.set_size m_image.width, m_image.height
        this.wheel_scrolled.push this.on_wheel
    }
    def on_wheel(e) {
        m_zoom -= e.dir
        if m_zoom < 0 {
            m_zoom = 0
        } elif m_zoom >= ZOOM_SCALE.size {
            m_zoom = ZOOM_SCALE.size - 1
        }
        m_pane.image = image_zoom(m_image, ZOOM_SCALE[m_zoom])
    }
}

def image_zoom(src:Image, zoom:Float)
{
    let dst = Image(Int(src.width.to_float() * zoom), Int(src.height.to_float() * zoom), "rgb")
    dst.fill_rect Color.WHITE
    dst.blend_resampled src
    return dst
}

var fname = null
if ARGV.size > 0 {
    fname = ARGV[0]
} else {
    let filter = [
        "All files:*.*",
        "JPEG file:*.jpg",
        "*.gif:GIF file",
        "*.png:PNG file",
        "*.bmp:Windows Bitmap",
        "*.webp:WebP image",
    ]
    fname = open_file_dialog("Select image file", filter)
    if !fname {
        return
    }
}
try {
    var f = MyForm(fname)
    f.visible = true
    f.wait()
} catch e:Error {
    alert e, "ImageViewer"
}

