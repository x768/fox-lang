import image
import math

def mandelbrot(width:Int, height:Int)
{
	let maxIteration = 64
	let cMinX = -2.0
	let cMaxX = 1.0
	let cMinY = -1.25
	let cMaxY = 1.25
	let fX = (cMaxX - cMinX) / width.to_float()
	let fY = (cMaxY - cMinY) / height.to_float()

	let img = Image(width, height, "L")

	for y in 0..height {
		let start_i = cMinY + fY * y.to_float()

		for x in 0..width {
			let start = Complex(cMinX + fX * x.to_float(), start_i)
			var c = start

			for i in 0..maxIteration {
				c = c * c + start
				if c.rho > 2.0 {
					img[x, y] = i * 255 / maxIteration
					break
				}
			}
		}
	}
	return img
}

let img = mandelbrot(800, 600)
img.save "mandelbrot.png"
